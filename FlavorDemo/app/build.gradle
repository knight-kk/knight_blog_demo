plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.wkk.flavordemo"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"
        missingDimensionStrategy "type", "phone"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "channel","pay_type"

    productFlavors {
        huawei {
            dimension "channel"
            missingDimensionStrategy "type", "pad"
            //这个必须在相同维度的配置

        }
        xiaomi {
            dimension "channel"
        }
        oppo {dimension "channel"}
        vivo {dimension "channel"}

        huaweiPay{
            dimension "pay_type"
            //这个必须在相同维度的配置
//            matchingFallbacks = ['pay1']
        }
        xiaomiPay{dimension "pay_type"
            matchingFallbacks = ['pay2']
        }
        commonPay{
            dimension "pay_type"
            matchingFallbacks = ['pay2']
        }
    }


    variantFilter { variant ->
        //获取变体中的风味
        def flavors = variant.flavors
        def channelFlavor=flavors[0].name
        def payTypeFlavor=flavors[1].name
        switch (payTypeFlavor){
            case "huaweiPay":
                if (channelFlavor != "huawei") setIgnore(true)/*忽略此变体*/
                break
            case "xiaomiPay":
                if (channelFlavor != "xiaomi") setIgnore(true)
                break
            case "commonPay":
                if (channelFlavor == "huawei" || channelFlavor == "xiaomi") setIgnore(true)
                break
        }
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(path: ':mylibrary')
    implementation project(path: ':mylibrary2')
//    implementation project(path: ':mylibrary')


//    huaweiImplementation "华为联运sdk xxxx"
//    xiaomiImplementation "小米联运sdk xxxx"

//    huaweiPayImplementation "华为联运sdk xxxx"
//    xiaomiPayImplementation "小米联运sdk xxxx"
//    commonPayImplementation "支付宝sdk xxxx"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}